apply plugin: 'maven'
apply plugin: 'signing'

def currentDir = buildscript.sourceFile.getParentFile()
apply from: "$currentDir/publish_to_maven_repo.gradle"

ext.mavenDeploymentVersion = isReleaseBuild() ? version.replace("-SNAPSHOT", "") : version

afterEvaluate { project ->
    task libraryJar(type: Jar, dependsOn:classes) {
        from sourceSets.main.output.classesDir
        if (configurations.findByName('compileAndIncludeClassesInLibraryJar') != null) {
            from configurations.compileAndIncludeClassesInLibraryJar.collect { it.isDirectory() ? it : zipTree(it) }
        }
        classifier = 'library'
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
        from javadoc.destinationDir
        classifier = 'javadoc'
    }

    artifacts {
        archives libraryJar
        archives sourcesJar
        archives javadocJar
    }

}
