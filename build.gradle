task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

allprojects {
    group = 'com.badlogicgames.gdxpay'
    version = '0.10.4-SNAPSHOT'
}

class Developer {
    String id
    String name
}

ext {
    androidBuildToolsVersion = '25.0.2' // version also referenced in .travis.yml.
    androidCompileSdkVersion = 24 // version also referenced in .travis.yml.
    androidMinimalSdkVersion = 8
    androidTargetSdkVersion = 24
    assertJVersion = '1.7.1'
    gdxVersion = '1.9.6'
    openIabVersion = '0.9.8.7'
    robolectricVersion = '4.3_r2-robolectric-0'
    junitVersion = '4.12'
    mockitoVersion = '1.10.19'
    jsr305Version = '3.0.1'
    developers = [
            new Developer(id: 'noblemaster', name: 'Christoph Aschwanden'),
            new Developer(id: 'keesvandieren', name: 'Kees van Dieren')
    ]
}

buildscript {
    ext {
        androidToolsVersion = '2.3.1'
        roboVMVersion = '1.12.0'
        moeVersion = '1.3.6'
        unmockVersion = "0.3.6"
    }

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${androidToolsVersion}"
        classpath "org.robovm:robovm-gradle-plugin:${roboVMVersion}"
        classpath "org.multi-os-engine:moe-gradle:${moeVersion}"
        classpath "de.mobilej.unmock:UnMockPlugin:${unmockVersion}"
    }
}

allprojects {
	  apply plugin: "eclipse"
    
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":gdx-pay-tests-iosmoe") {
    apply plugin: "moe"
    configurations { natives }
    dependencies {
        compile project(':gdx-pay-client')
        compile project(':gdx-pay-tests')
        compile project(':gdx-pay-iosmoe-apple')
        compile "com.badlogicgames.gdx:gdx-backend-moe:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}
